variable "name" {
  type        = string
  default     = "imperva-dsf-hub"
  description = "Deployment name"
  validation {
    condition     = length(var.name) > 3
    error_message = "Deployment name must be at least 3 characters"
  }
}

variable "subnet_id" {
  type        = string
  description = "Subnet id for the DSF hub instance"
  validation {
    condition     = length(var.subnet_id) >= 15 && substr(var.subnet_id, 0, 7) == "subnet-"
    error_message = "Subnet id is invalid. Must be subnet-********"
  }
}

variable "instance_type" {
  type        = string
  default     = "r6i.2xlarge"
  description = "Ec2 instance type for the DSF hub"
}

variable "disk_size" {
  default = 510
  validation {
    condition     = var.disk_size >= 500
    error_message = "DSF hub instance disk size must be at least 500GB"
  }
}

variable "ebs_provisioned_iops" {
  type        = number
  default     = 0
  description = "Provisioned IOPS for ebs volume"
}

variable "ebs_througput" {
  type        = number
  default     = 125
  description = "Throughput for ebs volume"
}

variable "web_console_sg_ingress_cidr" {
  type        = list(any)
  description = "List of allowed ingress cidr patterns for the DSF hub instance for web console"
  default     = []
}

variable "sg_ingress_cidr" {
  type        = list(any)
  description = "List of allowed ingress cidr patterns for the DSF hub instance for ssh and internal protocols"
}

variable "key_pair" {
  type        = string
  description = "key pair for DSF hub instance. This key must be generated by by the hub module and present on the local disk"
}

variable "installation_location" {
  type = object({
    s3_bucket = string
    s3_key    = string
  })
  description = "S3 DSF installation location"
  nullable    = false
}

variable "hadr_secondary_node" {
  type        = bool
  default     = false
  description = "Is this node a hadr secondary one"
}

variable "hadr_main_hub_sonarw_secret" {
  type = object({
    name = string
    arn  = string
  })
  default     = null
  description = "Secret of sonarw taken from the main hub node. This var must be defined for hadr seconday node"
}

variable "hadr_main_sonarw_public_key" {
  type        = string
  description = "SSH public key for sonarw user"
  default     = null
}

variable "admin_password" {
  type        = string
  sensitive   = true
  description = "Admin password"
  validation {
    condition     = length(var.admin_password) > 8
    error_message = "Admin password must be at least 8 characters"
  }
  nullable = false
}

variable "ssh_key_pair_path" {
  type        = string
  description = "SSH key path"
  nullable    = false
}

variable "dsf_base_ami_name_tag" {
  type     = string
  default = "RHEL-8.6.0_HVM-20220503-x86_64-2-Hourly2-GP2" # Exists on all regions
}

######################## Additional (optional) parameters ########################
# Use this param to specify any additional parameters for the initial setup, example syntax below
# variable "additional_install_parameters" { default = "--smtp-ssl --ignore-system-warnings" }
# https://sonargdocs.jsonar.com/4.5/en/sonar-setup.html#noninteractive-setup
variable "additional_install_parameters" {
  default = ""
}
